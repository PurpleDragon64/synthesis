pomdp

// arena definition
const int WIDTH = 7;
const int HEIGHT = 3;

const int xMIN = 0;
const int xMAX = WIDTH-1;
const int yMIN = 0;
const int yMAX = HEIGHT-1;

// holes
hole double h12 in {0.1, 0.2, 0.8, 0.9};
hole double h32 in {0.1, 0.2, 0.8, 0.9};
hole double h52 in {0.1, 0.2, 0.8, 0.9};
hole double h10 in {0.1, 0.2, 0.8, 0.9};
hole double h30 in {0.1, 0.2, 0.8, 0.9};
hole double h50 in {0.1, 0.2, 0.8, 0.9};

const int oy = 1;
hole int o1x in {1, 2, 3, 4, 5, 6};
hole int o2x in {1, 2, 3, 4, 5, 6};
hole int o3x in {1, 2, 3, 4, 5, 6};

// goal
formula goal = x=xMAX & y=yMIN & traped=false;
label "goal" = goal;

// movement
formula canGoRight = x<xMAX & traped=false;
formula canGoDown  = y>yMIN & traped=false;
formula canGoUp    = y<xMAX & traped=false;

formula leftOfObstacle = y=1 & (x=o1x-1|x=o2x-1|x=o3x-1);
formula aboveObstacle  = y=2 & (x=o1x|x=o2x|x=o3x);
formula belowObstacle  = y=0 & (x=o1x|x=o2x|x=o3x);

formula goRight = leftOfObstacle ? x : min(x+1, xMAX);
formula goDown  = aboveObstacle ? y : max(y-1, yMIN);
formula goUp    = belowObstacle ? y : min(y+1, yMAX);

formula noTrap = (x!=1 | y!=2) & (x!=3 | y!=2) & (x!=5 | y!=2) & (x!=1 | y!=0) & (x!=3 | y!= 0) & (x!=5 | y!=0);

// agent
module agent
    x : [xMIN..xMAX] init xMIN;
    y : [yMIN..yMAX] init yMAX;
    traped : bool init false;

    [r] !traped & noTrap -> (x'=goRight);
    [d] !traped & noTrap -> (y'=goDown);
    [u] !traped & noTrap -> (y'=goUp);

    [r] !traped & x=1&y=2 -> h12 : (x'=goRight)&(traped'=true) + 1-h12 : (x'=goRight);
    [d] !traped & x=1&y=2 -> h12 : (y'=goDown)&(traped'=true) + 1-h12 : (y'=goDown);
    [u] !traped & x=1&y=2 -> h12 : (y'=goUp)&(traped'=true) + 1-h12 : (y'=goUp);

    [r] !traped & x=3&y=2 -> h32 : (x'=goRight)&(traped'=true) + 1-h32 : (x'=goRight);
    [d] !traped & x=3&y=2 -> h32 : (y'=goDown)&(traped'=true) + 1-h32 : (y'=goDown);
    [u] !traped & x=3&y=2 -> h32 : (y'=goUp)&(traped'=true) + 1-h32 : (y'=goUp);

    [r] !traped & x=5&y=2 -> h52 : (x'=goRight)&(traped'=true) + 1-h52 : (x'=goRight);
    [d] !traped & x=5&y=2 -> h52 : (y'=goDown)&(traped'=true) + 1-h52 : (y'=goDown);
    [u] !traped & x=5&y=2 -> h52 : (y'=goUp)&(traped'=true) + 1-h52 : (y'=goUp);

    [r] !traped & x=1&y=0 -> h10 : (x'=goRight)&(traped'=true) + 1-h10 : (x'=goRight);
    [d] !traped & x=1&y=0 -> h10 : (y'=goDown)&(traped'=true) + 1-h10 : (y'=goDown);
    [u] !traped & x=1&y=0 -> h10 : (y'=goUp)&(traped'=true) + 1-h10 : (y'=goUp);

    [r] !traped & x=3&y=0 -> h30 : (x'=goRight)&(traped'=true) + 1-h30 : (x'=goRight);
    [d] !traped & x=3&y=0 -> h30 : (y'=goDown)&(traped'=true) + 1-h30 : (y'=goDown);
    [u] !traped & x=3&y=0 -> h30 : (y'=goUp)&(traped'=true) + 1-h30 : (y'=goUp);

    [r] !traped & x=5&y=0 -> h50 : (x'=goRight)&(traped'=true) + 1-h50 : (x'=goRight);
    [d] !traped & x=5&y=0 -> h50 : (y'=goDown)&(traped'=true) + 1-h50 : (y'=goDown);
    [u] !traped & x=5&y=0 -> h50 : (y'=goUp)&(traped'=true) + 1-h50 : (y'=goUp);

    [p] traped -> 1 : (traped'=false);

endmodule

// observables
observable "finished" = goal;
observable "traped" = traped;
observable "canGoRight" = canGoRight;
observable "canGoDown" = canGoDown;
observable "canGoUp" = canGoUp;

// rewards
rewards "penalty"
    true : 1;
    [p] true : 100;
endrewards